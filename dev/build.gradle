apply plugin: "groovy"
apply plugin: 'application'
apply plugin: 'fatjar'

jar.enabled = true
apply from: rootProject.file("compiler-settings.gradle")

// The main class of the application
mainClassName = 'uk.co.o2.stockservice.configuration.StockServiceBootStrap'

// Configure the fatJar task
fatJar {
    classifier 'fat'

    // We need to add the main class to the manifest, otherwise the JAR won't start.
    manifest {
        attributes 'Main-Class': mainClassName
    }

    // We have to exclude signature files from the fat JAR, otherwise we'll get invalid signature file digest errors.
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
}


// Set our project variables
project.ext {
    dropwizardVersion = '0.6.2'
    dropWizardConfig = './src/main/groovy/resources/stockService.yml'
}


dependencies {


    compile groovy
    compile dropWizard, jbossLogging

    compile group: 'com.yammer.dropwizard', name: 'dropwizard-hibernate', version: '0.6.2'
    compile group: 'com.yammer.dropwizard', name: 'dropwizard-migrations', version: '0.6.2'
    compile group: 'com.yammer.dropwizard', name: 'dropwizard-auth', version: '0.6.2'
    compile group: 'com.h2database', name: 'h2', version: '1.3.171'

    // this dependency is needed due to http://issues.gradle.org/browse/GRADLE-2647
    compile "com.sun.jersey:jersey-client:1.15"



}

compileJava.options.encoding = "ASCII"
compileTestJava.options.encoding = "ASCII"
compileGroovy.options.encoding = "ASCII"
compileTestGroovy.options.encoding = "ASCII"

compileGroovy.groovyOptions.fork(memoryMaximumSize: '1024m')
compileTestGroovy.groovyOptions.fork(memoryMaximumSize: '1024m')

test {
    inputs.property("alwaysExecute", java.util.UUID.randomUUID())
    useJUnit()
    jvmArgs '-Xms128m', '-Xmx512m', '-XX:MaxPermSize=256m'
}



// Configure the run task to start the Dropwizard service

run {
    args 'server', dropWizardConfig
}


task stage(dependsOn: ['clean', 'fatJar'])
